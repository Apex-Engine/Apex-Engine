void libapex_command_rename(int user, unitgroup u, string argv) {
	string name = argv;
	name = StringSub( name, StringFind(name, " ", c_stringCase) + 1, StringLength(name) ); // remainder of chat message

	UnitGroupLoopBegin(u);
	while(!UnitGroupLoopDone()) {
		libapex_unit_set_name(name, UnitGroupLoopCurrent());
		libapex_print_output(user, "Unit [ID: " + libapex_unit_id(UnitGroupLoopCurrent()) + "] name set to: " + name);
		libapex_unit_cache_update(UnitGroupLoopCurrent(), "@rename", argv);
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();

	return;
}
// libapex_command_say
// ==================
// prints [unit name]: msg to chat
// (actor talk icon and sound are defined in data.galaxy)
void libapex_command_say(int user, unitgroup u, string argv) {
	text unit_name;
	text unit_msg;
	string cmd_msg = argv;
	cmd_msg = ": " + libapex_string_trim(StringSub( cmd_msg, StringFind(cmd_msg, " ", c_stringCase) + 1, StringLength(cmd_msg) )); // remainder of chat message

	unit_msg = StringToText(cmd_msg);

	UnitGroupLoopBegin(u);
	while(!UnitGroupLoopDone()) {
		unit_name = libapex_unit_get_name( UnitGroupLoopCurrent() );

		// @say unit visuals (flash circle, make a talk icon appear overhead)
		UnitFlashSelection(UnitGroupLoopCurrent(), 2.0);
		ActorSend(libNtve_gf_MainActorofUnit(UnitGroupLoopCurrent()), "AttachModel " + libapex_TALK_ICON_ACTOR + "{} Overhead");

		libapex_print_text(PlayerGroupAll(), unit_name + unit_msg);

		// log msg in chatlog
		libapex_log(user, unit_name, cmd_msg);

		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();

	return;
}
// libapex_command_narrate
// ====================
// just prints out a line of text to everyone
void libapex_command_narrate(int user, unitgroup u, string argv) {
	string msg = argv;
	msg = StringSub( msg, StringFind(msg, " ", c_stringCase) + 1, StringLength(msg) ); // remainder of chat message

	libapex_print(PlayerGroupAll(), libapex_string_trim(msg));

	// log msg in chatlog
	libapex_log(user, StringToText(""), msg);

	return;
}
